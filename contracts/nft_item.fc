#include "imports/stdlib.fc";
#include "imports/op.fc";
#include "imports/params.fc";

;;
;;  TON NFT Item editable Smart Contract v2
;;  support ownership_assigned on minting nft
;;

{-

    NOTE that this tokens can be transferred within the same workchain.

    This is suitable for most tokens, if you need tokens transferable between workchains there are two solutions:

    1) use more expensive but universal function to calculate message forward fee for arbitrary destination (see `misc/forward-fee-calc.cs`)

    2) use token holder proxies in target workchain (that way even 'non-universal' token can be used from any workchain)

-}

int min_tons_for_storage() asm "50000000 PUSHINT"; ;; 0.05 TON

;;
;;  Storage
;;
;;  uint64 index
;;  MsgAddressInt collection_address
;;  MsgAddressInt owner_address
;;  cell content
;;  MsgAddressInt authority_address
;;  MsgAddressInt editor_address
;;  MsgAddressInt deployer_address
;;  uint64 revoked_at
;;

global int storage::index;
global int init?;
global slice storage::collection_address;
global slice storage::owner_address;
global cell storage::content;
global slice storage::authority_address;
global slice storage::editor_address;
global slice storage::deployer_address;
global int storage::revoked_at;

() load_data() impure inline {
    slice ds = get_data().begin_parse();

    storage::index              = ds~load_uint(64);
    storage::collection_address = ds~load_msg_addr();
    init?                       = false;

    if (ds.slice_bits() > 0) {
        init?                      = true;
        storage::owner_address     = ds~load_msg_addr();
        storage::content           = ds~load_ref();
        storage::authority_address = ds~load_msg_addr();
        storage::editor_address    = ds~load_msg_addr();
        storage::deployer_address  = ds~load_msg_addr();
        storage::revoked_at        = ds~load_uint(64);
    }
}

() store_data() impure inline {
    set_data(
        begin_cell()
            .store_uint(storage::index, 64)
            .store_slice(storage::collection_address)
            .store_slice(storage::owner_address)
            .store_ref(storage::content)
            .store_slice(storage::authority_address)
            .store_slice(storage::editor_address)
            .store_slice(storage::deployer_address)
            .store_uint(storage::revoked_at, 64)
        .end_cell()
    );
}

() send_msg(int flag, slice to_address, int amount, int op, int query_id, builder payload, int send_mode) impure inline {
    var msg = begin_cell()
            .store_uint(flag, 6)
            .store_slice(to_address)
            .store_coins(amount)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(op, 32)
            .store_uint(query_id, 64);

    if (~ builder_null?(payload)) {
        msg = msg.store_builder(payload);
    }

    send_raw_message(msg.end_cell(), send_mode);
}

() transfer_ownership(int my_balance, int index, slice collection_address, slice owner_address, cell content, 
                        slice editor_address, slice sender_address, int query_id, slice in_msg_body, int fwd_fees) impure inline {
    throw_unless(401, equal_slices(sender_address, owner_address));

    slice new_owner_address = in_msg_body~load_msg_addr();
    force_chain(new_owner_address);
    slice response_destination = in_msg_body~load_msg_addr();
    in_msg_body~load_int(1); ;; this nft don't use custom_payload
    int forward_amount = in_msg_body~load_coins();

    int rest_amount = my_balance - min_tons_for_storage();
    if (forward_amount) {
        rest_amount -= (forward_amount + fwd_fees);
    }
    int need_response = response_destination.preload_uint(2) != 0; ;; if NOT addr_none: 00
    if (need_response) {
        rest_amount -= fwd_fees;
    }

    throw_unless(402, rest_amount >= 0); ;; base nft spends fixed amount of gas, will not check for response

    if (forward_amount) {
        send_msg(0x10, new_owner_address, forward_amount, op::ownership_assigned(), query_id, 
                                begin_cell().store_slice(storage::owner_address).store_slice(in_msg_body), 1);  ;; paying fees, revert on errors
    }
    if (need_response) {
        force_chain(response_destination);
        send_msg(0x10, response_destination, rest_amount, op::excesses(), query_id, null(), 1); ;; paying fees, revert on errors
    }

    storage::owner_address = new_owner_address;

    store_data();
}

() transfer_editorship(int my_balance, int index, slice collection_address, slice owner_address, cell content, 
                        slice editor_address, slice sender_address, int query_id, slice in_msg_body, int fwd_fees) impure inline {
    throw_unless(401, equal_slices(sender_address, editor_address));

    slice new_editor_address = in_msg_body~load_msg_addr();
    force_chain(new_editor_address);
    slice response_destination = in_msg_body~load_msg_addr();
    in_msg_body~load_int(1); ;; this nft don't use custom_payload
    int forward_amount = in_msg_body~load_coins();

    int rest_amount = my_balance - min_tons_for_storage();
    if (forward_amount) {
        rest_amount -= (forward_amount + fwd_fees);
    }
    int need_response = response_destination.preload_uint(2) != 0; ;; if NOT addr_none: 00
    if (need_response) {
        rest_amount -= fwd_fees;
    }

    throw_unless(402, rest_amount >= 0); ;; base nft spends fixed amount of gas, will not check for response

    if (forward_amount) {
        send_msg(0x10, new_editor_address, forward_amount, op::editorship_assigned(), query_id, 
                                    begin_cell().store_slice(storage::editor_address).store_slice(in_msg_body), 1);  ;; paying fees, revert on errors
    }
    if (need_response) {
        force_chain(response_destination);
        send_msg(0x10, response_destination, rest_amount, op::excesses(), query_id, null(), 1); ;; paying fees, revert on errors
    }

    storage::editor_address = new_editor_address;

    store_data();
}

() request_owner(slice sender_address, int query_id, slice dest, cell body, int with_content) impure inline {
    var msg = begin_cell()
        .store_uint(storage::index, 64)
        .store_slice(sender_address)
        .store_slice(storage::owner_address)
        .store_ref(body)
        .store_uint(storage::revoked_at, 64)
        .store_uint(with_content, 1);

    if (with_content != 0) {
        msg = msg.store_ref(storage::content);
    }

        ;; mode 64 = carry all the remaining value of the inbound message
    send_msg(0x10 | 0x18, dest, 0, op::owner_info(), query_id, msg, 64);
}

() prove_ownership(int query_id, slice dest, cell body, int with_content) impure inline {
    var msg = begin_cell()
        .store_uint(storage::index, 64)
        .store_slice(storage::owner_address)
        .store_ref(body)
        .store_uint(storage::revoked_at, 64)
        .store_uint(with_content, 1);

    if (with_content != 0) {
        msg = msg.store_ref(storage::content);
    }

    send_msg(0x10 | 0x18, dest, 0, op::ownership_proof(), query_id, msg, 64);
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }
    slice sender_address = cs~load_msg_addr();

    cs~load_msg_addr(); ;; skip dst
    cs~load_coins(); ;; skip value
    cs~skip_bits(1); ;; skip extracurrency collection
    cs~load_coins(); ;; skip ihr_fee
    int fwd_fee = muldiv(cs~load_coins(), 3, 2); ;; we use message fwd_fee for estimation of forward_payload costs

    load_data();

    if (~ init?) {
        throw_unless(405, equal_slices(storage::collection_address, sender_address));
        storage::owner_address = in_msg_body~load_msg_addr();
        storage::content = in_msg_body~load_ref();
        storage::editor_address = in_msg_body~load_msg_addr();
        storage::authority_address = in_msg_body~load_msg_addr();
        storage::deployer_address = in_msg_body~load_msg_addr();
        storage::revoked_at = 0;

        store_data();

        if (in_msg_body.slice_data_empty?() == false) {
          var forward_amount = in_msg_body~load_coins();
          if (forward_amount) {
            send_msg(0x10, storage::owner_address, forward_amount, op::ownership_assigned(), 0, 
                                begin_cell().store_slice(storage::collection_address).store_slice(in_msg_body), 3); ;; paying fees, ignore errors
          }
        }
        return ();
    }

    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);

    if (op == op::request_owner()) {
        request_owner(sender_address, query_id, in_msg_body~load_msg_addr(), in_msg_body~load_ref(), in_msg_body~load_uint(1));
        return ();
    }

    if (op == op::prove_ownership()) {
        throw_unless(401, equal_slices(storage::owner_address, sender_address));
        prove_ownership(query_id, in_msg_body~load_msg_addr(), in_msg_body~load_ref(), in_msg_body~load_uint(1));
        return ();
    }

    if (op == op::revoke()) {
        throw_unless(401, equal_slices(storage::authority_address, sender_address));
        throw_unless(403, storage::revoked_at == 0);

        storage::revoked_at = now();
        store_data();
        return ();
    }

    if (op == op::transfer()) {
        transfer_ownership(my_balance, storage::index, storage::collection_address, storage::owner_address, 
                                    storage::content, storage::editor_address, sender_address, query_id, in_msg_body, fwd_fee);
        return ();
    }

    if (op == op::get_static_data()) {
        send_msg(0x10, sender_address, 0, op::report_static_data(), query_id, 
                                    begin_cell().store_uint(storage::index, 256).store_slice(storage::collection_address), 64);  ;; carry all the remaining value of the inbound message
        return ();
    }

    if (op == op::transfer_editorship()) {
        transfer_editorship(my_balance, storage::index, storage::collection_address, storage::owner_address, 
                                    storage::content, storage::editor_address, sender_address, query_id, in_msg_body, fwd_fee);
        return ();
    }

    if (op == op::edit_content()) {
        throw_unless(410, equal_slices(sender_address, storage::editor_address));
        storage::content = in_msg_body~load_ref();
        store_data();
        return ();
    }
    
    throw(0xffff);
}

;;
;;  GET Methods
;;

(int, int, slice, slice, cell) get_nft_data() method_id {
    load_data();
    return (init?, storage::index, storage::collection_address, storage::owner_address, storage::content);
}

slice get_editor_address() method_id {
    load_data();
    return storage::editor_address;
}

slice get_authority_address() method_id {
    load_data();
    return storage::authority_address;
}

slice get_deployer_address() method_id {
    load_data();
    return storage::deployer_address;
}

int get_revoked_time() method_id {
    load_data();
    return storage::revoked_at;
}
